import com.google.gson.Gson
import net.fabricmc.loom.task.RemapJarTask

buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
	}
}

plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower-mini' version '1.1.0'
	id "net.darkhax.curseforgegradle" version "1.0.7"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.mc_version}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/repurposed_structures.accesswidener")
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://api.modrinth.com/maven/" }
	maven { url "https://jitpack.io" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings minecraft.officialMojangMappings()
	
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation ("maven.modrinth:modmenu:${project.mod_menu_version}") { transitive = false }

	modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
	modImplementation include("com.github.Draylar.omega-config:omega-config-base:${project.omega_config_version}")
}

if (project.use_third_party_mods) {
	repositories {
		maven {	url "https://nexus.resourcefulbees.com/repository/maven-public/" }
		maven { url "https://www.cursemaven.com" }
	}

	dependencies {
		modRuntimeOnly "curse.maven:structure_void_toggle-357304:3488866"
		//modRuntimeOnly "com.telepathicgrunt:CommandStructures-Fabric:1.4.1+1.18.1"
		modRuntimeOnly "com.telepathicgrunt:FeatureNBTDeadlockBeGone-Fabric:2.0.0+1.18.2-rc1"

		//modRuntimeOnly "curse.maven:shutup_experimental_settings-401978:3545078"
		//modRuntimeOnly "curse.maven:cyanide-543444:3538092"

		//modRuntimeOnly "curse.maven:towersofthewild-552655:3633073"

		//modRuntimeOnly "curse.maven:fabric_waystones-410902:3479213"
		//modRuntimeOnly "curse.maven:charm-318872:3393290"
		//modRuntimeOnly "curse.maven:more_villagers-486080:3480631"
		//modRuntimeOnly "curse.maven:farmers_delight-482834:3466296"
		//modRuntimeOnly "curse.maven:better_strongholds-480684:3479839"
		//modRuntimeOnly "curse.maven:better_strongholds-480684:3479839"
		//modRuntimeOnly "curse.maven:yungs_api-421649:3479840"
	}
}

apply from: 'gradle/processresources.gradle'

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

apply from: 'gradle/maven.gradle'
apply from: 'gradle/modrinth.gradle'
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = "$System.env.CURSEFORGEKEY"
	int projectId = 391366
	String changelogText = new File("CHANGELOG.md").getText('UTF-8')

	def mainFile = upload(projectId, remapJar)
	mainFile.changelog = changelogText
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "release"
	mainFile.displayName = "Repurposed Structures v${project.mod_version} (${project.minecraft_version} MC)"
	mainFile.addRequirement("cloth-config")
	mainFile.addRequirement("fabric-api")
	mainFile.addRequirement("no-null-processors")
	mainFile.addRequirement("feature-nbt-deadlock-be-gone")
}


task buildAndRelease{}
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.publishModrinth.mustRunAfter tasks.publish
tasks.publishCurseForge.dependsOn("remapJar")
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishCurseForge, tasks.publishModrinth)