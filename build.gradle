import net.fabricmc.loom.task.RemapJarTask

buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
		classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
	}
}

plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower-mini' version '1.1.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.mc_version}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/repurposed_structures.accesswidener")
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://api.modrinth.com/maven/" }
	maven { url "https://jitpack.io" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings minecraft.officialMojangMappings()
	
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation ("maven.modrinth:modmenu:${project.mod_menu_version}") { transitive = false }

	modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
	modImplementation include("com.github.Draylar.omega-config:omega-config-base:${project.omega_config_version}")
}

if (project.use_third_party_mods) {
	repositories {
		maven {	url "https://nexus.resourcefulbees.com/repository/maven-public/" }
		maven { url "https://www.cursemaven.com" }
		//flatDir { dirs 'deps' }
	}

	dependencies {
		modRuntime "com.telepathicgrunt:LoadNbtBlock:3.0.0+1.18-pre5"
		modRuntime "curse.maven:structure_void_toggle-357304:3488866"
		//modRuntime "com.telepathicgrunt:Blame-Fabric:1.17-4.0.0-fabric"
		//runtimeOnly "al:cyanide-fabric:1.17.1-1.2.1-pre.1-dev"

		//modRuntime "curse.maven:fabric_waystones-410902:3479213"
		//modRuntime "curse.maven:charm-318872:3393290"
		//modRuntime "curse.maven:more_villagers-486080:3480631"
		//modRuntime "curse.maven:farmers_delight-482834:3466296"
		//modRuntime "curse.maven:better_strongholds-480684:3479839"
		//modRuntime "curse.maven:better_strongholds-480684:3479839"
		//modRuntime "curse.maven:yungs_api-421649:3479840"
	}
}

apply from: 'gradle/processresources.gradle'

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

apply from: 'gradle/maven.gradle'
apply from: 'gradle/curseforge.gradle'
apply from: 'gradle/modrinth.gradle'

task buildAndRelease{}
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.curseforge, tasks.publishModrinth)