plugins {
	id 'fabric-loom' version '0.6-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.fabricmc.net" }
}

minecraft {
	accessWidener "src/main/resources/repurposed_structures.accesswidener"
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"

	implementation ('com.github.modmuss50:Voyager:1.0.0') {
		transitive = false
	}
	include ('com.github.modmuss50:Voyager:1.0.0') {
		transitive = false
	}

	modImplementation ('com.github.Draylar:angerable-patch:1.1.0-1.16.4') {
		transitive = false
	}
	include ('com.github.Draylar:angerable-patch:1.1.0-1.16.4') {
		transitive = false
	}
}

if (project.use_third_party_mods) {
	repositories {
		maven { url = "https://jitpack.io" }
		flatDir { dirs 'deps' }
	}

	dependencies {

		modImplementation ("io.github.prospector:modmenu:${project.mod_menu_version}") {
			transitive = false
		}

		modRuntime ("com.telepathicgrunt.loadnbtblock:loadnbtblock:1.0.8") {
			transitive = false
		}

		modRuntime ("com.telepathicgrunt.structure_void_toggle:structure_void_toggle-1.16.5:2.0.0") {
			transitive = false
		}

		modRuntime ("com.telepathicgrunt.blame:blame-1.16.4:2.4.0") {
			transitive = false
		}

	//	modRuntime "com.charm:charm-fabric-1.16.4:2.2.2"
	//	modRuntime "com.moandjiezana.toml:toml4j:0.7.2"
	//	modRuntime "com.electronwill.night-config:core:3.6.2"
	//	modRuntime "com.electronwill.night-config:toml:3.6.2"

	//	modRuntime "com.github.jellysquid3:lithium_fabric:1.16.1"
	//	modRuntime "com.github.jellysquid3:phosphor_fabric:1.16.1"
	//	modRuntime "com.github.jellysquid3:sodium_fabric:1.16.1"
	//	modRuntime "zabi.minecraft.nbttooltip:nbttooltip:1.0.4"
	//	modRuntime "com.github.voronoiaoc:byg:1.0.6"
	}
}


import groovy.json.JsonSlurper
import groovy.json.JsonOutput

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}

	// Minifies all .json files when building the mod.
	// Source files are not minified, only the jar copies.
	doLast {

		def jsonMinifyStart = System.currentTimeMillis()
		def jsonMinified = 0
		def jsonBytesSaved = 0

		fileTree(dir: outputs.files.asPath, include: '**/*.json').each {

			File file = it
			jsonMinified++
			def oldLength = file.length()
			try{
				file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
			}
			catch(Exception e){
				System.out.println("Parsing JSON error in " + file.path)
				throw e
			}
			jsonBytesSaved += oldLength - file.length()
		}

		println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}